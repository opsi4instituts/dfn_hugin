; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/

[Actions]

include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	comment "Show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$
	
	if FileExists("%ScriptPath%\delsub64.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub64.opsiscript"
	endif
	
	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."

	comment "Copy files"
	Files_install /64Bit
	Set $InstallDir$ = $InstallDir$ + "\bin\"
	Set $PathToExe$  = $InstallDir$ + "hugin.exe"
	
	if FileExists($PathToExe$)
		comment "Successful Installation"
	else
		logError "Fatal: cannot find $PathToExe$ after installation"
		isFatalError
	endif
	
	comment "Patch Registry"
	Registry_install /64Bit
	
	comment "Create shortcuts"
	LinkFolder_install_programs

	set $DesktopIcon$ = getProductProperty("desktopicon","no")
	if $DesktopIcon$ = "yes"
		comment "Create common desktop icon"
		Linkfolder_install_desktopicon
	endif
	
	comment "include custom post install file"
	set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$	
		endif
	endif
endif

[Files_install]
copy -s "%ScriptPath%\files\*.*" "$InstallDir$"

[Registry_install]
; da wir keinen Installer nehmen, mÃ¼ssen ein paar notwendige Keys angelegt werden, damit sie unter 'Systemeinstellungen' > 'Software' Hugin auftaucht
openkey [$RegPathUninstall$\$ProductId$]
set "DisplayName" = REG_SZ:"$ProductId$ OPSI-Version: $ProductVersion$"
set "DisplayIcon" = REG_SZ:"$PathToExe$"
set "InstallLocation" = REG_SZ:"$InstallDir$"
set "UninstallDirectory" = REG_SZ:"$InstallDir$"
;UninstallString ist der entscheidende Eintrag, ob es unter Systemsteuerung > Software angezeigt wird
;da es in diesem Fall keine Deinstallationsroutine gibt, rufen wir einfach hugin.exe auf ;-)
set "UninstallString" = REG_SZ:"$PathToExe$"
set "NoRepair" = REG_DWORD:0001
set "NoModify" = REG_DWORD:0001

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
	name: $PrettyName$
	target: $PathToExe$
end_link

[LinkFolder_install_programs]
set_basefolder common_startmenu
set_subfolder "Programs"
set_link
	name: $PrettyName$
	target: "$PathToExe$"
end_link

[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	if FileExists("%ScriptPath%\delsub64.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub64.opsiscript"
	endif
	
	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."

	comment "Start setup program"
	Winbatch_install_msi
	if stringToBool(isMsiExitcodeFatal($ExitCode$, "True", $ErrorString$))
		isFatalError
	endif

	comment "Test for installation success"
	; Ergebnis ist StringList (mehrere Einträge möglich);
	; in einer Zeile werden drei Ergebnisse mit "|" abgetrennt geliefert: Registrykey (mit Pfad) | Bereich Registry (kann direkt getRegistryValue übergeben werden | DisplayName
	; z.B. HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Test64|64bit|Test Registry Entry64
	Set $ResultList$ = SearchRegistryUninstallKeys($ProductName$)

	if ( count ($ResultList$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in registry!"
		isFatalError
	else
		Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
		Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))

		comment "Check version"
		; ProductVersion von OPSI-Paket "2018.0"; DisplayVersion in Registry aber "20.18.0"
		; Workaround: alle Punkte löschen, dann sind sie vergleichbar
		Set $InstalledVersion$ = stringReplace(GetRegistryValue($RegId$, "DisplayVersion",$RegBitScope$),".","")
		Set $ProductVersion$ = stringReplace($ProductVersion$,".","")
		if not ($InstalledVersion$ = $ProductVersion$)
			LogError "Wrong version number found, something went wrong."
			isFatalError
		endif

		comment "Get 'InstallLocation' from registry for path to exe"
		Set $InstallDir$ = GetRegistryValue($RegId$, "InstallLocation",$RegBitScope$)
		Set $PathToExe$ = $InstallDir$ + "\bin\hugin.exe"
	endif

	if $DesktopLink$ = "true"
		comment "Create common desktop link"
		Linkfolder_create_desktoplink
	endif
	
	comment "include custom post install file"
	set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$	
		endif
	endif
endif

[Winbatch_install_msi]
msiexec $SilentOption$ /i "%ScriptPath%\$InstFile$" /l* "$LogDir$\$ProductId$.install_log.txt" ALLUSERS=1 REBOOT=ReallySuppress

[Linkfolder_create_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
	name: $ProductName$
	target: $PathToExe$
end_link

